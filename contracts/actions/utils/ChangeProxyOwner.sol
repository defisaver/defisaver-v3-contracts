// SPDX-License-Identifier: MIT

pragma solidity =0.8.24;

import { ActionBase } from "../ActionBase.sol";
import { DFSProxyRegistryController } from "../../utils/DFSProxyRegistryController.sol";
import { DSAuth } from "../../DS/DSAuth.sol";

/// @title Changes the owner of the DSProxy and updated the DFSRegistry
contract ChangeProxyOwner is ActionBase {

    /// @param newOwner Address of the new owner
    struct Params {
        address newOwner;
    }

    DFSProxyRegistryController constant dfsRegController =
        DFSProxyRegistryController(DFS_REG_CONTROLLER_ADDR);
    
    /// @inheritdoc ActionBase
    function executeAction(
        bytes memory _callData,
        bytes32[] memory _subData,
        uint8[] memory _paramMapping,
        bytes32[] memory _returnValues
    ) public payable virtual override returns (bytes32) {
        Params memory inputData = parseInputs(_callData);

        inputData.newOwner = _parseParamAddr(inputData.newOwner, _paramMapping[0], _subData, _returnValues);

        _changeOwner(inputData.newOwner);

        return bytes32(bytes20(inputData.newOwner));
    }

    function executeActionDirect(bytes memory _callData) public payable override {
        Params memory inputData = parseInputs(_callData);

        _changeOwner(inputData.newOwner);
    }

    /// @inheritdoc ActionBase
    function actionType() public pure virtual override returns (uint8) {
        return uint8(ActionType.STANDARD_ACTION);
    }

    //////////////////////////// ACTION LOGIC ////////////////////////////

    function _changeOwner(address _newOwner) internal {
        require(_newOwner != address(0), "Owner is empty address");

        DSAuth(address(this)).setOwner(_newOwner);
        
        dfsRegController.changeOwnerInDFSRegistry(_newOwner);
    }

    function parseInputs(bytes memory _callData) public pure returns (Params memory params) {
        params = abi.decode(_callData, (Params));
    }
}
